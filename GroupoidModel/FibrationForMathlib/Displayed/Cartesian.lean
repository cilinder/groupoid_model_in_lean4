/-
Copyright (c) 2024 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/

import GroupoidModel.FibrationForMathlib.Displayed.Fibre
import GroupoidModel.FibrationForMathlib.Displayed.Basic
import Mathlib.CategoryTheory.Limits.Preserves.Basic

/-!
# Cartesian lifts

There are also typeclasses `Display.Cartesian` and `Display.CoCartesian`
carrying data witnessing that a given lift is cartesian and cocartesian, respectively.

Specialized to the display category structure of a functor `P : E ‚•§ C`,
we obtain the class `CartMor` of cartesian morphisms in `E`.
The type `CartMor P` is defined in terms of the predicate `isCartesianMorphism`.

In this file we shall refer to a hom-over `g : X ‚ü∂[f] Y` as a "lift" of
`f : I ‚ü∂ J` to `X : F I` and `Y : F J`, since the map application of cartesianness concerns
the display structure of a functor `P : E ‚•§ C`, where `I J : C` are objects of the base category `C`.

We prove the following closure properties of the class `CartMor` of cartesian morphisms:
- `cart_id` proves that the identity morphism is cartesian.
- `cart_comp` proves that the composition of cartesian morphisms is cartesian.
- `cart_iso_closed` proves that the class of cartesian morphisms is closed under isomorphisms.
- `cart_pullback` proves that, if `P` preserves pullbacks, then
the pullback of a cartesian morphism is cartesian.

`instCatCart` provides a category instance for the class of cartesian morphisms,
and `Cart.forget` provides the forgetful functor from the category of cartesian morphisms
to the domain category `E`.

-/

set_option autoImplicit true

namespace CategoryTheory

open Category Opposite Functor Limits Cones

variable {C E : Type*} [Category C] {F : C ‚Üí Type*} [Display F]

namespace Display

variable {I J : C} {f : I ‚ü∂ J} {X : F I} {Y : F J}

/-- A hom-over `g : X ‚ü∂[f] Y` is cartesian if for every morphism `u : K ‚ü∂ I`
in the base and every hom-over `g' : Z ‚ü∂[u ‚â´ f] Y` over the composite
 `u ‚â´ f`, there is a unique morphism `k : Z ‚ü∂[u] X` over `u` such that
 `k ‚â´ g = g'`.
```
       _ _ _ _ _ _ _ _ _ _ _
      /         [g']        \
     |                      v
     Z - - - - > X --------> Y
     _   ‚àÉ![k]   _   [g]     _
     |           |           |
     |           |           |
     v           v           v

     K --------> I --------> J
          u            f
```
-/
class Cartesian (g : X ‚ü∂[f] Y) where
  uniq_lift : ‚àÄ ‚¶ÉK : C‚¶Ñ ‚¶ÉZ : F K‚¶Ñ (u : K ‚ü∂ I) (g' : Z ‚ü∂[u ‚â´ f] Y),
  Unique {k : Z ‚ü∂[u] X // (k ‚â´‚Çó g) = g'}

/-- A morphism `g : X ‚ü∂[f] Y` over `f` is cocartesian if for all morphisms `u` in the
base and `g' : X ‚ü∂[f ‚â´ u] Z` over the composite `f ‚â´ u`, there is a unique morphism
`k : Y ‚ü∂[u] Z` over `u` such that `g ‚â´ k = g'`.
```
       _ _ _ _ _ _ _ _ _ _ _
      /         [g']        \
     |                      v
     X ------- > Y - - - - > Z
     _   [g]     _   ‚àÉ![k]   _
     |           |           |
     |           |           |
     v           v           v

     I --------> J --------> K
          f            u
```
-/
class CoCartesian (g : X ‚ü∂[f] Y) where
  uniq_lift : ‚àÄ ‚¶ÉK : C‚¶Ñ ‚¶ÉZ : F K‚¶Ñ (u : J ‚ü∂ K) (g' : X ‚ü∂[f ‚â´ u] Z),
  Unique {k : Y ‚ü∂[u] Z // (g ‚â´‚Çó k) = g'}

namespace Cartesian

open Display

variable (g : X ‚ü∂[f] Y) [Cartesian g] {K : C} {Z : F K}

/-- `gap g u g'` is the canonical map from a lift `g' : Z ‚ü∂[u ‚â´ f] X` to a
cartesian lift `g` of `f`. -/
def gap (u : K ‚ü∂ I) (g' : Z ‚ü∂[u ‚â´ f] Y) : Z ‚ü∂[u] X :=
  (Cartesian.uniq_lift (g:= g) (Z:= Z) u g').default.val

/-- A variant of `gaplift` for `g' : Z ‚ü∂[f'] X` with casting along
`f' = u ‚â´ f` baked into the definition. -/
def gapCast (u : K ‚ü∂ I) {f' : K ‚ü∂ J} (g' : Z ‚ü∂[f'] Y) (w : f' = u ‚â´ f) :
  Z ‚ü∂[u] X :=
  (Cartesian.uniq_lift (g:= g) (Z:= Z) u (w ‚ñ∏ g')).default.val

@[simp]
lemma gap_cast (u : K ‚ü∂ I) {f' : K ‚ü∂ J} (g' : Z ‚ü∂[f'] Y)
    (w : f' = u ‚â´ f) : gapCast g u g' w = gap g u (w ‚ñ∏ g') := by
  rfl

/-- The composition of the gap lift and the cartesian hom-over is the given hom-over. -/
@[simp]
lemma gap_prop (u : K ‚ü∂ I) (g' : Z ‚ü∂[u ‚â´ f] Y) :
    ((gap g u g') ‚â´‚Çó g) = g' :=
  (Cartesian.uniq_lift (f:= f) (g:= g) (Z := Z) u g').default.property

/-- The uniqueness part of the universal property of the gap lift. -/
@[simp]
lemma gaplift_uniq {u : K ‚ü∂ I} (g' : Z ‚ü∂[u ‚â´ f] Y) (v : Z ‚ü∂[u] X)
  (hv : v ‚â´‚Çó g = g') : v = gap (g:= g) u g' := by
  simp [gap]
  rw [‚Üê (Cartesian.uniq_lift u g').uniq ‚ü®v,hv‚ü©]

/-- The identity hom-over is cartesian. -/
instance instId {X : F I} : Cartesian (ùüô‚Çó X) where
  uniq_lift := fun K Z u g' => {
    default := ‚ü®(comp_id u) ‚ñ∏ g', by simp‚ü©
    uniq := by aesop
  }

/-- Cartesian based-lifts are closed under composition. -/
instance instComp {X : F I} {Y : F J} {Z : F K} {f‚ÇÅ : I ‚ü∂ J} {f‚ÇÇ : J ‚ü∂ K}
  (g‚ÇÅ : X ‚ü∂[f‚ÇÅ] Y) [Cartesian g‚ÇÅ] (g‚ÇÇ : Y ‚ü∂[f‚ÇÇ] Z) [Cartesian g‚ÇÇ] :
  Cartesian (g‚ÇÅ ‚â´‚Çó g‚ÇÇ) where
  uniq_lift := fun I' W u g' => {
    default := ‚ü® gap g‚ÇÅ u (gap g‚ÇÇ (u ‚â´ f‚ÇÅ) (assoc u f‚ÇÅ f‚ÇÇ ‚ñ∏ g')), by
      rw [‚Üê Display.cast_assoc_symm, gap_prop g‚ÇÅ _ _, gap_prop g‚ÇÇ _ _]
      simp ‚ü©
    uniq := by
      intro ‚ü®l, hl‚ü©
      simp
      apply gaplift_uniq
      apply gaplift_uniq
      simp [assoc_cast, hl] }

end Cartesian

/-- The type of cartesian lifts of a morphism `f` with fixed target. -/
class CartLift (f : I ‚ü∂ J) (tgt : F J) extends Lift f tgt where
  is_cart : Cartesian homOver

/--Mere existence of a cartesian lift with fixed target. -/
def HasCartLift (f : I ‚ü∂ J) (tgt : F J) := Nonempty (CartLift f tgt)

end Display

end CategoryTheory
